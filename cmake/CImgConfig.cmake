SET(CIMG_LINK_LIBRARIES)
SET(CIMG_INCLUDE_DIRS)

# Lapack support
OPTION(WITH_LAPACK "enable lapack support for CImg" ${UNIX})
IF(WITH_LAPACK)
	ENABLE_LANGUAGE(Fortran)
	FIND_PACKAGE(LAPACK)
	IF(LAPACK_FOUND)
		ADD_DEFINITIONS(-Dcimg_use_lapack)
		LIST(APPEND CIMG_LINK_LIBRARIES ${LAPACK_LIBRARIES})
	ELSE(LAPACK_FOUND)
		MESSAGE(SEND_ERROR "Lapack cannot be used, please disable WITH_LAPACK")
	ENDIF(LAPACK_FOUND)
ENDIF(WITH_LAPACK)

IF(WIN32)
	LIST(APPEND CIMG_LINK_LIBRARIES gdi32)
ENDIF(WIN32)

IF(UNIX)
	IF(APPLE)
		OPTION(WITH_CARBON "Use Carbon framework" ON)
	ENDIF(APPLE)
	IF(WITH_CARBON)
		# Carbon framework support
		ADD_DEFINITIONS(-Dcimg_display=3)
	ELSE(WITH_CARBON)
		# X11 support
		FIND_PACKAGE(X11 REQUIRED QUIET)
		LIST(APPEND CIMG_LINK_LIBRARIES pthread ${X11_LIBRARIES})
		IF(X11_Xrandr_LIB)
			ADD_DEFINITIONS(-Dcimg_use_xrandr)
			LIST(APPEND CIMG_LINK_LIBRARIES ${X11_Xrandr_LIB})
		ENDIF(X11_Xrandr_LIB)
		IF(X11_XShm_INCLUDE_PATH)
			ADD_DEFINITIONS(-Dcimg_use_xshm)
		ENDIF(X11_XShm_INCLUDE_PATH)
	ENDIF(WITH_CARBON)
ENDIF(UNIX)

# OpenMP support
FIND_PACKAGE(OpenMP QUIET)
IF(OPENMP_FOUND)
	# deactivate openmp in windows by default as it is not
	# supported in Visual Studio Express and will drop you
	# into dll hell
	IF(MSVC)
		OPTION(WITH_OPENMP "use OpenMP" OFF)
	ELSE(MSVC)
		OPTION(WITH_OPENMP "use OpenMP" ON)
	ENDIF(MSVC)
ENDIF(OPENMP_FOUND)
IF(WITH_OPENMP)
	ADD_DEFINITIONS(-Dcimg_use_openmp ${OpenMP_CXX_FLAGS})
	IF(CMAKE_COMPILER_IS_GNUCXX)
		LIST(APPEND CIMG_LINK_LIBRARIES gomp)
	ENDIF(CMAKE_COMPILER_IS_GNUCXX)
ENDIF(WITH_OPENMP)

# PNG support
FIND_PACKAGE(PNG QUIET)
IF(PNG_FOUND)
	OPTION(WITH_PNG "Use png libraries" ON)
	IF(WITH_PNG)
		ADD_DEFINITIONS(-Dcimg_use_png)
		LIST(APPEND CIMG_LINK_LIBRARIES ${PNG_LIBRARIES})
		LIST(APPEND CIMG_INCLUDE_DIRS ${PNG_INCLUDE_DIR})
	ENDIF(WITH_PNG)
ENDIF(PNG_FOUND)

# JPEG support
FIND_PACKAGE(JPEG QUIET)
IF(JPEG_FOUND)
	OPTION(WITH_JPEG "Use jpeg libraries" ON)
	IF(WITH_JPEG)
		ADD_DEFINITIONS(-Dcimg_use_jpeg)
		LIST(APPEND CIMG_LINK_LIBRARIES ${JPEG_LIBRARIES})
		LIST(APPEND CIMG_INCLUDE_DIRS ${JPEG_INCLUDE_DIR})
	ENDIF(WITH_JPEG)
ENDIF(JPEG_FOUND)

# don't let exception windows pop up 
ADD_DEFINITIONS(-Dcimg_verbosity=0)

MACRO(CimgLibInfo)
	MESSAGE(STATUS "    Lapack       : ${WITH_LAPACK}")
	IF(APPLE)
		MESSAGE(STATUS "    Carbon       : ${WITH_CARBON}")
	ENDIF(APPLE)
	MESSAGE(STATUS "    OpenMP       : ${WITH_OPENMP}")
	MESSAGE(STATUS "    PNG          : ${WITH_PNG}")
	MESSAGE(STATUS "    JPEG         : ${WITH_JPEG}")
ENDMACRO()
